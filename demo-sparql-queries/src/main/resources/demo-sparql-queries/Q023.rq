#id:NXQ_00023
#title:Proteins that are involved in transport and located in a membrane and that are not glycosylated (experimentally or predicted)
#tags:subcellular location,cellular process,function,PTM
#count:1541
#time:3.514

select distinct ?entry where {
# cv:KW-0472 membrane
# cv:SL-0162 membrane
# cv:KW-0813 transport
# cv:GO_0006810 transport
# KW-0325 glycoprotein
  ?entry :isoform  ?iso .
  {
        ?iso :goBiologicalProcess / :term / :childOf cv:GO_0006810.
  } union {
    ?iso :keyword / :term cv:KW-0813.
  }
  {
        ?iso :keyword / :term cv:KW-0472.
  } union {
        ?iso :cellularComponent / :term /:childOf cv:SL-0162.
  }  
  filter not exists { ?iso :keyword / :term cv:KW-0325 } 
}
