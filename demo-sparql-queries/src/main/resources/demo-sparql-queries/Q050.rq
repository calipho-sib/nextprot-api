#title:Proteins which are expressed in brain according to IHC but not expressed in brain according to microarray
#tags:expression,experimental evidence
#acs:Q9Y6N5, Q9Y6J6, A1L4K1, ...
#count:9730
#time:33.8

select distinct ?entry where {
  ?entry :isoform ?iso.
  ?iso :expressionProfile ?s1;:expressionProfile ?s2.

# there exists an evidence saying that microarray didn't detect any expression in the brain
  ?s2 :term/:childOf cv:TS-0095;:negativeEvidence/:experimentalContext/:detectionMethod cv:ECO_0000220.

# there exists an evidence telling that the protein is expressed in brain according to IHC
  ?s1 :term/:childOf cv:TS-0095;:evidence/:experimentalContext/:detectionMethod cv:ECO_0000045.

}
