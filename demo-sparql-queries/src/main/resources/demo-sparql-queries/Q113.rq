#title: Q113  Proteins with PE1 and at least one proteotypic peptide
#ac:A0A5B9,A0AV02,A0AV96
#tags:peptide,proteotypic,long
#count:14927
#time:369

# Could be much faster if we attach a triple to peptides telling if it is proteotypic or not !
select distinct ?entry where {
  ?entry :existence :Evidence_at_protein_level .
  ?entry :isoform / :peptideMapping ?pm .
  ?pm :evidence / :reference ?ref .
  ?ref :provenance db:PeptideAtlas .
  ?ref :accession ?ac .
  filter not exists {
    ?entry2 :isoform / :peptideMapping ?pm2 .
    ?pm2 :evidence / :reference ?ref2 .
    ?ref2 :provenance db:PeptideAtlas .
    ?ref2 :accession ?ac .
    filter (?entry2 != ?entry)
  }
}

order by ?entry
