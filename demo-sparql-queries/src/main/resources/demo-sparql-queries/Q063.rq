#title:Proteins that have one or more RRM RNA-binding domain and either no GO "RNA binding" or a GO "RNA binding" with evidence IEA or ISS
#tags:region,domain,evidence
#acs:Q14103, Q6ZRY4, Q8TC92, ...
#count:78
#time: 0.1

select distinct ?entry where {
 ?entry :isoform ?iso
 {
#>=1 RRM RNA-binding domain
  ?iso :region/:term cv:DO-00581.

#GO "RNA binding" with evidence IEA or ISS
  FILTER NOT EXISTS {
   ?iso :function/:term /:childOf cv:GO_0003723
  }
 }UNION{
#>=1 RRM RNA-binding domain
  ?iso :region/:term cv:DO-00581.

#GO "RNA binding" with evidence IEA or ISS
  ?iso :function ?s1.
  ?s1 :term /:childOf cv:GO_0003723;:evidence/:code ?type.
  values ?type {:IEA :ISS}

 }
}

