#title:Q020, Proteins with >=2 HPA antibodies whose genes are located on chromosome 21 and that are highly expressed at IHC level in heart
#acs:P56181,P48449,Q8TCY5
#tags:selected,expression,antibody,gene
#count:3
#time:1.269
#pending
select ?entry  where {
  select distinct ?entry ?ac where {
    
    ?entry :gene / :chromosome "21"^^xsd:string .

    ?entry :isoform / :expressionProfile ?s1.
    ?s1 :in / :childOf  term:TS-0445 .
    ?s1 :withEvidence ?evi .
    ?evi :expressionLevel :High .
    ?evi :experimentalContext / :detectionMethod term:ECO_0000045.

    ?entry :isoform / :mapping ?map .
    ?map a :AntibodyMapping .
    ?map :reference ?ref .
	?ref :provenance db:HPA; :accession ?ac .
  }
} group by ?entry 
having (count(?ac)>=2)
